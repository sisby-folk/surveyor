//file:noinspection GroovyAssignabilityCheck
//file:noinspection GroovyAccessibility
buildscript {
    dependencies {
        classpath "com.modrinth.minotaur:Minotaur:${libs.versions.minotaur.get()}"
        classpath "com.github.breadmoirai:github-release:${libs.versions.githubRelease.get()}"
    }
}


plugins {
    id 'maven-publish'
    alias libs.plugins.loom
}

version = "$project.baseVersion+$project.branch"
archivesBaseName = project.name
group = 'folk.sisby'

repositories {
    maven { url 'https://repo.sleeping.town/' } // Kaleido
}

dependencies {
    minecraft libs.mc
    mappings variantOf(libs.yarn) { classifier "v2" }
    modImplementation libs.fl
    modImplementation libs.fapi
    modImplementation libs.kaleidoConfig

    include libs.kaleidoConfig
}

processResources {
    final Map<String, String> map = [
            "version": version.toString(),
            "mc"     : libs.versions.mc.get(),
            "fl"     : libs.versions.fl.get(),
            "fapi"   : libs.versions.fapi.get()
    ]

    inputs.properties(map)
    filesMatching('*.mod.json') { expand(map) }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.release = 17
}

java {
    withSourcesJar()
}

loom {
    accessWidenerPath = file("src/main/resources/surveyor.accesswidener")
}

jar {
    from("LICENSE.txt") {
        rename { "${it}_${archivesBaseName}" }
    }
}

publishing {
    repositories {
        maven {
            name 'SleepingTown'
            url 'https://repo-api.sleeping.town/'
            credentials(PasswordCredentials)
        }
    }

    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

if (System.getenv("MODRINTH_TOKEN")) {
    apply plugin: "com.modrinth.minotaur"
    modrinth {
        token = System.getenv("MODRINTH_TOKEN")
        projectId = project.name
        versionNumber = project.version
        versionName = baseVersion
        versionType = 'release'
        uploadFile = remapJar
        versionType = "alpha"
        gameVersions = [libs.versions.mc.get()]
        loaders = ['quilt', 'fabric']
        detectLoaders = false
        dependencies {
            required.version libs.fapi.get().getName(), libs.versions.fapi.get()
        }
        changelog = rootProject.file("CHANGELOG.md").text + "\n\n\nChangelog: https://github.com/sisby-folk/$project.name/releases/tag/v$baseVersion"
        syncBodyFrom = rootProject.file("README.md").text
    }
}

if (System.getenv("GITHUB_TOKEN")) {
    apply plugin: "com.github.breadmoirai.github-release"
    githubRelease {
        token System.getenv("GITHUB_TOKEN")
        owner "sisby-folk"
        repo project.name
        tagName "v$baseVersion"
        releaseName baseVersion
        targetCommitish defaultBranch
        draft false
        prerelease false
        releaseAssets remapJar.archiveFile
        allowUploadToExisting true
        generateReleaseNotes true
        body rootProject.file("CHANGELOG.md").text + "\n"
    }
    tasks.githubRelease.dependsOn remapJar
}

if (System.getenv("MODRINTH_TOKEN") && System.getenv("GITHUB_TOKEN")) {
    tasks.register("fullRelease") {
        group "publishing"
        dependsOn "githubRelease"
        dependsOn "modrinth"
    }
}
